!
! NAME
!     write_ladder_network
!
! DESCRIPTION
!       write spice circuit file for the ladder network
!
! SEE ALSO
!
!
! HISTORY
!
!     started 23/01/09 CJS
!
SUBROUTINE write_ladder_network(CFterm,CFtype,CF_dim,max_order,R_add,nw,wmin,wmax,wnorm_save)

USE type_specifications
USE general_module
USE constants
USE frequency_spec
USE filter_module

IMPLICIT NONE

integer CF_dim,max_order

real(dp) :: CFterm(1:CF_dim,1:5)
integer  :: CFtype(1:CF_dim)
real(dp) :: R_add
integer  :: nw
real(dp) :: wmin,wmax,wnorm_save

! local variables

real(dp) :: R,L,C,L2,K
integer  :: type

integer  :: i,loop

integer  :: next_node,node1,node2,last_series_node,inode1,inode2,R_add_node,ref_node

!START

! write the continued fraction to the screen
    
  write(*,*)'Write the spice circuit file for the ladder network'
  write(*,*)'      component     type          R           L           C '
  do loop=1,max_order 
    write(*,'(2I12,A5,4ES12.4)')loop,CFtype(loop),'     ',CFterm(loop,1),CFterm(loop,2),CFterm(loop,3)
  end do
  
  write(*,*)'R_add=',R_add
  
  open(unit=50,file='ngspice_circuit.cir')
  open(unit=40,file='impedance.lib')
  
  write(50,'(A)')'Spice circuit model for rational transfer function impedance'
  write(50,'(A)')'* generated by equiv_circuit_test.F90'
  
! This initial stuff is always required
  write(50,'(A)')'*'
  write(50,'(A)')'IS 0  1 AC  1.0 0.0'
  write(50,'(A)')'*'

! include the sub-circuit for the impedance model and link it into the spice circuit file
  write(50,'(A)')'* Link to impedance sub-circuit'
  write(50,'(A)')'x_impedance_model 1 0 impedance_model'
  write(50,'(A)')'*'
  write(50,'(A)')".INCLUDE './impedance.lib'"
  write(50,'(A)')'*'
   
! write the sub-cirucit model
  write(40,'(A)')'* Spice sub-circuit model for impedance created by NETWORK_SYNTHESIS software'
  write(40,'(A)')'* https://github.com/chrissmartt/network_synthesis'
  write(40,'(A)')'* '
  write(40,'(A)')'.subckt impedance_model 1 10000'   ! nodes 1 (=last_series_node) and 10000 (=ref_node)
  
  last_series_node=1
  next_node=2
  ref_node=10000
    
  do loop=1,max_order 
  
    type=CFtype(loop)
    
! series_RLC= 1            
! series_LC=  2     
! series_RC=  3     
! series_RL=  4    
! series_C=   5     
! series_L=   6     
! series_R=   7     
! series_BRUNE=   8   
!
! shunt_RLC= -1            
! shunt_LC=  -2     
! shunt_RC=  -3     
! shunt_RL=  -4    
! shunt_C=   -5     
! shunt_L=   -6     
! shunt_R=   -7     
   

    if (type.GT.0) then
! a series component so connect to a new node or the last component connects to the reference
      node1=last_series_node
      if (loop.NE.max_order) then
        node2=next_node
        next_node=next_node+1
      else
        node2=ref_node  
      end if
      last_series_node=node2
    else if (type.LT.0) then
      node1=last_series_node
      node2=ref_node   
    end if
    
    R=CFterm(loop,1)
    L=CFterm(loop,2)
    C=CFterm(loop,3)
    L2=CFterm(loop,4)
    K=CFterm(loop,5)
    
    if (type.EQ.series_RLC) then   

      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'L',loop,' ',node1,node2,L
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'C',loop,' ',node1,node2,C
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'R',loop,' ',node1,node2,R
    
    else if (type.EQ.series_LC) then   

      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'L',loop,' ',node1,node2,L
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'C',loop,' ',node1,node2,C
    
    else if (type.EQ.series_RC) then   

      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'C',loop,' ',node1,node2,C
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'R',loop,' ',node1,node2,R
    
    else if (type.EQ.series_RL) then   

      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'L',loop,' ',node1,node2,L
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'R',loop,' ',node1,node2,R
    
    else if (type.EQ.series_C) then   

      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'C',loop,' ',node1,node2,C
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'R',loop,' ',node1,node2,1E8
    
    else if (type.EQ.series_L) then   

      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'L',loop,' ',node1,node2,L
    
    else if (type.EQ.series_R) then   

      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'R',loop,' ',node1,node2,R
    
    else if (type.EQ.series_BRUNE) then   

      next_node=next_node+1
      inode1=next_node
      next_node=next_node+1
      inode2=next_node
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'R',loop   ,' ',node1,inode1,R
      next_node=next_node+1
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'L1',loop  ,' ',inode1,inode2,L
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'C',loop   ,' ',inode2,0 ,C
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'Rbig',loop,' ',inode2,0 ,1E8
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'L2',loop  ,' ',node2,inode2,L2
! note directions of inductor specifications      
      write(40,'(A,I2.2,A,I2.2,A,I2.2,A,ES12.4)')'K',loop ,' L1',  &
                                             loop,' L2',loop,' ',K
      
    
    else if (type.EQ.shunt_RLC) then   

      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'L',loop,' ',node1      ,next_node ,L
      next_node=next_node+1
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'C',loop,' ',next_node-1,next_node ,C
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'R',loop,' ',next_node,node2     ,R
      next_node=next_node+1
    
    else if (type.EQ.shunt_LC) then   

      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'L',loop,' ',node1      ,next_node ,L
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'C',loop,' ',next_node,node2     ,C
      next_node=next_node+1
    
    else if (type.EQ.shunt_RC) then   

      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'R',loop,' ',node1      ,next_node ,R
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'C',loop,' ',next_node,node2     ,C
      next_node=next_node+1
      
    else if (type.EQ.shunt_RL) then   

      next_node=next_node+1
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'R',loop,' ',node1      ,next_node ,R
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'L',loop,' ',next_node,node2     ,L
      next_node=next_node+1
      
    else if (type.EQ.shunt_L) then   

      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'L',loop,' ',node1,node2     ,L
      
    else if (type.EQ.shunt_C) then   

      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'C',loop,' ',node1,node2     ,C
      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'R',loop,' ',node1,node2,1E8
      
    else if (type.EQ.shunt_R) then   

      write(40,'(A,I2.2,A,I6,I6,ES12.4)')'R',loop,' ',node1,node2     ,R
   
    end if
           
    write(40,'(A)')'*'
           
  end do  ! next element of the network
  write(40,'(A)')'*'
! End the sub-circit
  write(40,'(A)')'.ends'
  
! Complete the ngspice test circuit

  if (R_add.GT.0d0) then
! we need an additional branch for R_add
    write(*,*)'Adding additional resistance branch'
    next_node=next_node+1
    R_add_node=next_node
    write(50,'(A)')'*'
    write(50,'(A,I4,A)')'IS2 0  ',next_node,' AC  1.0 0.0'
    write(50,'(A)')'*'      
    write(50,'(A,I4,I4,ES12.4)')'R_add',0,R_add_node,R_add
  else
    R_add_node=0
  end if
  
! THIS FOR A LINEAR FREQUENCY SCALE
!  write(50,'(A,I8,2ES12.4)')'.AC LIN ',nw,wnorm_save*wmin/(6.28318530718),wnorm_save*wmax/(6.28318530718)
  
! THIS FOR A LOG FREQUENCY SCALE
  write(50,'(A,I8,2ES12.4)')'.AC DEC ',nw,wnorm_save*wmin/(6.28318530718),wnorm_save*wmax/(6.28318530718)
  
  write(50,'(A)')'*'
  if (R_add.GT.0d0) then
    write(50,'(A,I4,A)')'.PRINT ac  V( 1 , ',R_add_node,' )'
  else
    write(50,'(A)')'.PRINT ac  V( 1 )'
  end if
  write(50,'(A)')'*'
  write(50,'(A)')'*'
  write(50,'(A)')'.END'
  
  close(unit=40)
  close(unit=50)
  
  
  
  RETURN
  END SUBROUTINE write_ladder_network

